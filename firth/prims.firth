char prims.firth drop
char see_comment_block_below_for_copyright_notice drop

char : define compile
	char %s parse define compile
interpret compilebuf buildfunc bindfunc immediate
: nextword
	char %s parse
interpret compilebuf buildfunc bindfunc
: postpone
	nextword call
interpret compilebuf buildfunc bindfunc immediate
: ;
	postpone interpret compilebuf buildfunc bindfunc
interpret compilebuf buildfunc bindfunc immediate
: ;immed postpone ; postpone immediate ; immediate

: // char \n parse drop ;immed // There, now we can comment our code!

// /////////////////////////////////////////////////////////////////////////////
// prims.firth
// Basic primitives for low level code and compiler manipulation
// Copyright Â© 2015 Brigham Toskin
// <https://github.com/IonoclastBrigham/firth/blob/master/LICENSE>
// /////////////////////////////////////////////////////////////////////////////

: ( char ) parse drop ;immed // inline, single-line comments

// a few parser/interpreter/compiler control words
: [ ( -- ) postpone interpret ;immed
: ] ( -- ) compile ;
: eval ( -- ... )
	postpone [ // TODO: preserve previous value on cstack?
	nextword call
	]
;immed

// values and variables
: " ( -- str ) char " parse push ;immed
: const: ( val -- ) postpone : push postpone ; ;immed
// : var: ( val -- ) ;

// some basic arithmetic operators and shortcuts
: + ( a b -- a+b ) char + binop ;immed
: - ( a b -- a-b ) char - binop ;immed
: * ( a b -- a*b ) char * binop ;immed
: / ( a b -- a/b ) char / binop ;immed
: % ( a b -- a%b ) char % binop ;immed
: ^ ( a b -- a^b ) char ^ binop ;immed
// TODO: alias or inline these instead of calling
: 1+ ( n -- n+1 ) 1 + ;
: inc ( n -- n+1 ) 1+ ;
: 1- ( n -- n-1 ) 1 - ;
: dec ( n -- n-1 ) 1- ;
: -1* ( n -- -n ) -1 * ;
: neg ( n -- -n ) -1* ;
: 2* ( n -- 2n ) 2 * ;
: 2/ ( n -- n/2 ) 2 / ;

// some basic boolean operators
: > ( a b -- a>b ) char > binop ;immed
: < ( a b -- a<b ) char < binop ;immed
: = ( a b -- a==b ) " ==" binop ;immed
: <= ( a b -- a<=b ) " <=" binop ;immed
: >= ( a b -- a>=b ) " >=" binop ;immed
: ~= ( a b -- a!=b ) " ~=" binop ;immed
: and ( a b -- a&&b ) " and" binop ;immed
: or ( a b -- a||b ) " or" binop ;immed
: xor dup 2 pick and rot rot or not or if false else true end ; // FIXME!
